;========================================================
;				 Модуль инициализации
;========================================================

init:

	;-------------------------- Инициализация стека
	
	ldi		r17, RAMEND			; Выбор адреса вершины стека 
	out		SPL, r17			; Запись его в регистр стека

	;-------------------------- Инициализация Главного предделителя

	ldi		r17, 0x80		    ; Записываем число $80 в регистр temp
	out		CLKPR, r17			; Записываем в регистр CLKPR
	ldi		r17, 0x0			; Записываем 0 в регистр temp
	out		CLKPR, r17			; Записываем этот ноль в CLKPR

	;--------------------------- Инициализация компаратора

	ldi 	r17, 0x80			; Выключение компаратора
	out		ACSR, r17

	;-------------------------- Инициализация портов ВВ для чипа TM1367

	sbi		DDRB, TM1637_CLK
	sbi		DDRB, TM1637_DATA

	;-------------------------- Инициализация портов ВВ для чипа DS1302

	sbi		DDR_DS1302, DS1302_RST
	sbi		DDR_DS1302, DS1302_SCLK

	;-------------------------- Инициализация портов ВВ для кнопок

	cbi		DDR_BUTTON_MODE, BUTTON_MODE
	sbi		PORT_BUTTON_MODE, BUTTON_MODE

	cbi		DDR_BUTTON_SET, BUTTON_SET
	sbi		PORT_BUTTON_SET, BUTTON_SET

	;-------------------------- Инициализация таймеров

	ldi		r17, (1 << WGM12) | (1 << CS12) | (0 << CS11) | (1 << CS10) ; Выбор режима таймера (СТС, предделитель = 1024) 
	out		TCCR1B, r17
	ldi		r17, high(kdel2)	; Старший полубайт кода совпадения
	out		OCR1AH, r17			; Запись в регистр совпадения старш.
	ldi		r17, low(kdel2)		; Младший полубайт кода совпадения
	out		OCR1AL, r17			; Запись в регистр совпадения младш.

	ldi		r17, (1 << WGM01)							 ; Выбор режима таймера СТС 
	out		TCCR0A, r17
	ldi		r17, (1 << CS02) | (0 << CS01) | (1 << CS00) ; Выбор предделителя = 1024 
	out		TCCR0B, r17
	ldi		r17, kdel3
	out		OCR0A, r17

	;--------------------------- Разрешаем прерывание от таймеров
		
	ldi 	r17, (1 << OCIE1A) | (1 << OCIE0A)
	out		TIMSK, r17

	;--------------------------- Разрешаем прерывание INT0 и INT1 по заднему фронту

	ldi		r17, (0 << ISC00) | (1 << ISC01) | (0 << ISC10) | (1 << ISC11)
	out		MCUCR, r17

	ldi		r17, (1 << INT0) | (1 << INT1)
	out		GIMSK, r17