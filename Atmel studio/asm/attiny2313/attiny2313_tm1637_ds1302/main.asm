;
; attiny2313_tm1637.asm
;
; Created: 30.06.2020 14:09:01
; Author : verbkinm
;
; Описание:	
;	Отображение времени и даты на 4-х елементном дисплее с чипом TM1637, модуль времени ds1302. 		
;	Управление тремя кнопками, есть будильник.

.include "tn2313adef.inc"
.include "def_equ.inc"

;========================================================
;			Глобальные переменные в РОН
;========================================================

.def	CONST_ZERO		= r0		; Постоянный ноль

.def	timer0_counter	= r1		; Счетчик для TIM0 при отображении двоеточия
.def	timer0_counter_alarm_unlock	= r2	; счетчик для разблокировки будильника


.def	alarm			= r3		; Будильник выключен == 0,  включён == 1
.def	alarm_lock		= r4		; Блокировка будильника, блокирован == 1, разблокирован == 0. 
									; Блокировка будильника на N-ное время после его отключения, чтобы он не зацикливался.

.def	tm_bright_level = r5		; Уровень яркости дисплея, 0..7

.def	clock_mode		= r20		; Хранение состояния текущего отображения (время == 0, дата == 1, год == 2)
.def	mode			= r21		; Хранение позиции элемента, который устанавливается 
									; (ничего не устанавливается == 0, часы == 1, минуты == 2, число == 3, месяц == 4, 
									; год == 5, будильник вкл\выкл == 6, часы будильника == 7, минуты будильника == 8,
									; моргать временем во время сигнала будильника == 9)

.def	BYTE			= r22		; Байт передачи\приём данных на\из ds1302


;========================================================
;			Глобальные переменные в EEPROM
;========================================================

.eseg								; Выбираем сегмент EEPROM
	.org	0x00					; Устанавливаем текущий адрес сегмента

;------------------------- Хранение данных в упакованном формате для будильника

bcd_alarm_hours:	.db		0x07
bcd_alarm_minutes:	.db		0x30

;========================================================
;				Глобальные переменные в RAM
;========================================================

.dseg								; Выбираем сегмент RAM
	.org	0x60					; Устанавливаем текущий адрес сегмента

;------------------------- Хранение данных в упакованном формате от DS1302

bcd_seconds:	.byte	1			;
bcd_minutes:	.byte	1			;
bcd_hours:		.byte	1			;
bcd_day:		.byte	1			;
bcd_month:		.byte	1			;
bcd_week_day:	.byte	1			;
bcd_year:		.byte	1			;

;------------------------- Хранение подготовленных данных для отображения на TM1637 поэлементно

tm_s1:			.byte	1			; Десятки секунд
tm_s2:			.byte	1			; Единицы секунд

tm_m1:			.byte	1			; Десятки минут
tm_m2:			.byte	1			; Единицы минут

tm_h1:			.byte	1			; Десятки часов
tm_h2:			.byte	1			; Единицы часы

tm_d1:			.byte	1			; Десятки дня месяца 
tm_d2:			.byte	1			; Единицы дня месяца

tm_mt1:			.byte	1			; Десятки месяца
tm_mt2:			.byte	1			; Единицы месяца

tm_wd1:			.byte	1			; Десятки день недели 
tm_wd2:			.byte	1			; Единицы день недели 

tm_y3:			.byte	1			; Десятки года
tm_y4:			.byte	1			; Единицы года

;-------------------------- Последовательность следующих переменных важна!

tm_y1:			.byte	1			; Тысячи года, тут будет фиксированное значение дальше по коду
tm_y2:			.byte	1			; Сотни года, тут будет фиксированное значение дальше по коду

tm_ah1:			.byte	1			; Десятки часов будильника
tm_ah2:			.byte	1			; Единицы часов будильника
tm_am1:			.byte	1			; Десятки минут будильника
tm_am2:			.byte	1			; Единицы минут будильника

;========================================================
;			 Начало программного кода
;========================================================

.cseg		 						; Выбор сегмента программного кода
	.org	0						; Установка текущего адреса на ноль

start:	

	.include "interrupts_vector.asm"
	.include "interrupts.asm"
	.include "initialization.asm"

	;-------------------------- Очистка всех регистров, кроме регистра r0 и Z

	clr		CONST_ZERO

	ldi		ZH, 0
	ldi		ZL, 2

	ldi		r17, 29					
	mov		r1, r17

	clear_reg:
		dec		r1
		breq	clear_reg_end
		st		Z+, CONST_ZERO
		rjmp	clear_reg
	clear_reg_end:	

	;-------------------------- Очистка RAM под наши переменные

	ldi		ZL, 0x60
	ldi		r17, 22					; У нас 21 переменная = 22 минус первый декремент в цикле

	clear_ram:
		dec		r17
		breq	clear_ram_end
		st		Z+, CONST_ZERO
		rjmp	clear_ram
	clear_ram_end:	

	;-------------------------- Первые две цифры отображения года (тысячи и сотни) для TM1637

	ldi		ZH, high(tm_y1)
	ldi		ZL, low(tm_y1)

	ldi		r17, char_2
	st		Z+, r17
	ldi		r17, char_0
	st		Z+, r17

	;-------------------------- Считывание времени будильника из EEPROM для отображения на дисплее TM1637

	ldi		r17, bcd_alarm_hours
	rcall	EEPROM_read
	rcall	conv_ds1302_to_tm1637
	st		Z+, r16
	st		Z+, r15

	ldi		r17, bcd_alarm_minutes
	rcall	EEPROM_read
	rcall	conv_ds1302_to_tm1637
	st		Z+, r16
	st		Z, r15

	;-------------------------- В Proteus с минимальной яркостью ничего не отображается 

	ldi		r17, 1
	mov		tm_bright_level, r17

	sei								; Разрешение прерываний

	;-------------------------- Считать данные с DS1302

	rcall	DS1302_read_package_data

	;-------------------------- Основной бесконечный цикл в ожидании прерывания

	main:	
		sleep
		rjmp	main

.include "alarm.asm"
.include "ds1302.asm"
.include "tm1637.asm"
.include "lib.asm"

;-------------------------- Массив максимального числа в месяцах. Убрано в конец кода.

max_day_in_month:	.db		31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31
